ئورد jlexer من '//'؛

ئورد مكون، عنصرمخصص، عنصر، عنصرئشتمل، عنصرمربعنص؛
ئورد حدت، رجكس؛
ئورد مجال من مصفوفة؛

ئعلن _مفتاحيات = ['ئورد'، 'ك'، 'من'، 'ئعلن'، 'دع'، 'ئرجع'، 'طالما'، 'لكل'، 'ئدا'،
	'تم'، 'في'، 'حاول'، 'ئستدرك'، 'ريتما'، 'يمدد'، 'يملك'، 'وعد'، 'وئلا']؛
ئعلن _معاملات = ['عدم'، 'صحيح'، 'خطئ'، 'ئشطب'، 'ئو'، 'وو'، 'فارغ'، 'ليس']؛

ئعلن مسترمز ():
	يمدد مكون()؛
	
	يملك نصية يمين؛
	يملك منطق للقرفقط؛
	يملك نصية طول؛
	
	هدا.يمين = هدا.ردخاصية('يمين') ئو '0'؛
	هدا.للقرفقط = هدا.لهخاصية('للقرفقط')؛
	هدا.طول = هدا.ردخاصية('طول') ئو '60vh'؛
	
	نصية _مصدر = هدا.ردخاصية('مصدر')؛
	
	هدا._حالة = {
		مصدر: _مصدر.عووضكل('<br>'، '\n') ئدا _مصدر وئلا عدم
	}؛
تم

ئعلن مسترمز.هييئمصدر () ك نصية:
	نصية مهيئ = ''؛
	دع ليكسر = jlexer.lexer؛

	ليكسر.setInput(هدا._حالة.مصدر)؛
	
	دع توكن = ليكسر.lex()؛
	طالما توكن لا= 1:
		نصية مفردة = ليكسر.match؛
		ئدا توكن == 'KEYWORD':
			مهيئ = مهيئ + (<حيز صنف="جني-مفتاحية">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'OPERATOR':
			مهيئ = مهيئ + (<حيز صنف="جني-معامل">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'SYMBOL':
			مهيئ = مهيئ + (<حيز صنف="جني-رمز">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'STRING':
			مهيئ = مهيئ + (<حيز صنف="جني-تنصيص">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'NUMBER':
			مهيئ = مهيئ + (<حيز صنف="جني-رقم">%{مفردة}</حيز>)؛
		وئلا ئدا توكن == 'COMMENT':
			مهيئ = مهيئ + (<حيز صنف="جني-تعليق">%{مفردة}</حيز>)؛
		وئلا:
			مهيئ = مهيئ + مفردة؛
		تم
		توكن = ليكسر.lex()؛
	تم
	ئرجع مهيئ؛
تم

ئعلن مسترمز.عندضغطمفتاح (منوع هدف، حدت ح):
	ئدا هدا.للقرفقط:
		ح.ئمنعمفترض()؛
		ئرجع؛
	تم
	عنصرمربعنص تحرير = هدا.ئستعلم('.تحرير')؛
	
	ئدا ح:
		ئدا (ح ك منوع).keyCode == 9: #طابة
			قل 'طابة'؛
			ح.ئمنعمفترض()؛
			عدد بدا = تحرير.بدتحديد؛
			عدد نها = تحرير.نهتحديد؛
			#ئجعل قيمة المربعنص = ماقبل المئشر + فراغ + مابعده
			تحرير.قيمة = تحرير.قيمة.ردقطعة(0، بدا) + '\t' + تحرير.قيمة.ردقطعة(نها)؛
			#ضبط موضع المئشر بعد دلك
			تحرير.بدتحديد = بدا + 3؛ #رقم 3 هو حجم محرف الفراغ
			تحرير.نهتحديد = بدا + 3؛
		تم
	تم
تم

ئعلن مسترمز.عندتركمفتاح (منوع هدف، حدت ح):
	عنصرمربعنص تحرير = هدا.ئستعلم('.تحرير')؛
	هدا.خدحالة({
		مصدر: تحرير.قيمة
	})؛
تم

ئعلن مسترمز.ئمرر (عنصرمربعنص محرر):
	عنصرئشتمل تضليل = هدا.ئستعلم('.تضليل')؛
	عنصر سطرين = هدا.ئستعلم(".رقمسطرين")؛
	تضليل.مررئعلا = محرر.مررئعلا؛
	تضليل.مرريسار = محرر.مرريسار؛
	سطرين.مررئعلا = محرر.مررئعلا؛
تم

ئعلن مسترمز.ئضبطحدتين ():
	عنصرئشتمل تحرير = هدا.ئستعلم('.تحرير')؛
	ئدا تحرير:
		دع نفسه = هدا؛
		تحرير.ئضفمستمعحدت('ضغطمفتاح'، دالة (عنصر هدف، حدت ح): هدا.عندضغطمفتاح(هدف، ح))؛
		تحرير.ئضفمستمعحدت('تركمفتاح'، دالة (عنصر هدف، حدت ح): هدا.عندتركمفتاح(هدف، ح))؛
		تحرير.ئضفمستمعحدت('تمرير'، دالة (عنصر هدف، حدت ح): هدا.ئمرر(تحرير))؛
	تم
تم

ئعلن مسترمز.حددت ():
	عنصرمربعنص تحرير = هدا.ئستعلم('.تحرير')؛
	عنصرئشتمل تضليل = هدا.ئستعلم('.تضليل')؛
	عنصر سطرين = هدا.ئستعلم(".رقمسطرين")؛
	ئدا ليس هدا._حالة.مصدر:
		هدا._حالة.مصدر = ''؛
	تم
	ئدا ليس سطرين:
		هدا.حددتمشهد()؛
		هدا.ئضبطحدتين()؛
	تم
	عدد كمسطر = هدا._حالة.مصدر.قسسم('\n').طول؛
	عدد رقم؛
	
	ئدا سطرين:
		سطرين.ئشتملداخلي = (
			<تكرار لكل="رقم" في="مجال\(0, كمسطر\)">
				<قسم>%{رقم+1}</قسم>
			</تكرار>
		)؛
	تم
	
	ئدا تحرير:
		تحرير.قيمة = هدا._حالة.مصدر؛
		تضليل.ئشتملداخلي = هدا.هييئمصدر()؛
		عدد طول = كمسطر × 24؛
		
		تحرير.سمة['height'] = طول؛
		تضليل.سمة['height'] = طول؛
		
		هدا.ئمرر(تحرير)؛
	تم
تم

ئعلن مسترمز.ردسمات () ك نصية:
	ئرجع "
    .شطركود {
		width: 100%;
		height: %{هدا.طول};
		position: relative;
		max-height: %{هدا.طول};
		right:5px;
		line-height:24px;
		font-size:12px;
		letter-spacing: 1.4px !important;
    }
    .تحرير, .تضليل {
        width: 100%;
		font-size: 100%;
        margin: 0;
        padding: 0;
        position: absolute;
        border: 0;
        right:5px;
        overflow-y: auto;
        overflow-x: auto;
        scrollbar-height: none;
        color: #e8eaed;
        max-height: %{هدا.طول};
        height: %{هدا.طول};
		letter-spacing: 1.4px !important;
		-moz-tab-size : 3;
		-o-tab-size : 3;
		tab-size : 3;
    }
    .تضليل::-webkit-scrollbar, .تحرير::-webkit-scrollbar {
		height: 0;
    }
    .تحرير {
		z-index: 1;
		resize: none;
		color: transparent;
		background: transparent;
		caret-color: white;
		white-space: nowrap;
		line-height: 24px;
		font-size: 100%;
		font-size: 14px;
    }
    .تحرير:focus {
		border: 0;
		outline: 0;
    }
	.تضليل {
		z-index: 0;
		font-size: 100%;
		font-size: 14px;
    }
	.تظليل:focus {
		border: 0;
		outline: 0;
	}
	.رقمسطرين {
		text-align: left;
		color: gray;
		overflow: hidden;
		position: relative;
		max-height: %{هدا.طول};
		right: 8px;
		font-size: 100%;
		padding-left: 14px;
		line-height: 24px;
	}
	.رقمسطرينخطئ {
		color: white;
		background-color: rgb(153 27 27);
	}
	.سطرين {
		width: 100%;
		font-size: 100% !important;
		padding-right: 2px;
	}
	.سطرين .badge {
		font-weight: bold;
		font-size: 80% !important;
	}
	.جني-مفتاحية {
		color: #dfc47d;
		
	}
	.جني-معامل {
		color: #EC4D37;
	}
	.جني-تنصيص {
		color: #cc9393;
	}
	.جني-رقم {
		color: #8CD0D3;
	}	
	.جني-شباك {
		background-color: #f8f9fa;
		padding-left: 5px;
		padding-right: 5px;
	}
	.جني-تعليمة {
		background-color: rgba(50,51,55,255);
		color: lightslategray;
	}
	.جني-تعليق{
		color: #7f9f7f;
	}
	.جني-ئشتمل {
		background-color: gray
	}
	.جني-رمز {
		color: #9f9d6d;
	}"؛
تم

ئعلن مسترمز.صيير ():
	ئدا ليس هدا._حالة.مصدر:
		ئرجع (<فجوة></فجوة>)؛
	تم
	
	عدد رقم؛
	نصية سطرين = هدا._حالة.مصدر.قسسم('\n')؛
	عدد كمسطر = سطرين.طول؛
	عدد[] رقمين = مجال(0، كمسطر)؛
	ئرجع (
		<سمة>%{هدا.ردسمات\(\)}</سمة>
		<قسم صنف="رقمسطرين">
			<تكرار لكل="رقم" في="رقمين">
				<قسم صنف="">%{رقم+1}</قسم>
			</تكرار>
		</قسم>
		<قسم صنف="شطركود">
			<مربعنص صنف="تحرير">%{هدا._حالة.مصدر}</مربعنص>
			<منسق صنف="تضليل">%{هدا.هييئمصدر\(\)}</منسق>
		</قسم>
	)؛
تم

ئعلن مسترمز.حممل ():
	هدا.سمة['display'] = 'flex'؛
	هدا.ئضبطحدتين()؛
	#ئدا ليس هدا._حالة.مصدر:
	#	هدا._حالة.مصدر = هدا.ئشتملداخلي.عووضكل('\n'، '<br>').عووض('<فغ/>'، ' ')؛
	#	هدا.حددتمشهد()؛
	#	هدا.حددت()؛
	#وئلا:
	#	هدا.حددت()؛
	#تم
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مسترمز'، مسترمز)؛
تم
