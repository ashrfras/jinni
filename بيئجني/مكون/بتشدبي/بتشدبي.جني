ئورد مفترض ك __بتشدبي من '//cdn.jsdelivr.net/npm/pouchdb@8.0.1/+esm'؛
ئورد مفترض ك __بتشفايند من '//cdn.jsdelivr.net/npm/pouchdb-find@8.0.1/+esm'؛
ئورد وضيفة؛


# بنية معلومات قاعدة البيانات
ئعلن بنية معلوقاب:
	نصية ئسمقاب، #ئسم قاعدة البيانات
	عدد كموتيقة، #عدد الوتيقات في القاعدة
	نصية مضيف #ئسم مضيف القاعدة
تم

ئعلن بنية نتجقاب:
	نوعبنية[] وتيقات،
	نصية تحدير،
	نوعبنية ئحصاتنف،
	نصية بوكمارك
تم

ئعلن بنية خيائستنسخ:
	منطق حي، #live
    منطق حاولكر، #retry
    وضيفة فلتر، #filter
    نصية[] معرفات، #doc_ids
    نوعبنية معطيات، #query_params
    نصية مشهد، #view
    نوعبنية حدد، #selector
    عدد مند، #since
    عدد دقتقلب، #heartbeat
    عدد مهلة، #timeout
    عدد حجمدفعة، #batch_size
    عدد حددفعة #batches_limit
تم

ئعلن _قموس = {حي:'live'، حاولمج:'retry'، فلتر:'filter'، معرفات:'doc_ids'، معطيات:'query_params'، 
	مشهد:'view'، حدد:'selector'، مند:'since'، دقتقلب:'heartbeat'، مهلة:'timeout'، 
	حجمدفعة:'batch_size'، حددفعة:'batches_limit'}؛


ئعلن _بتشدبي (نصية ئسم، نوعبنية ئستيق؟):
	يختصر __بتشدبي؛
تم

ئعلن بتشدبي (نصية نصئتصال):
	يملك بتش؛
	
	ئدا نصئتصال.يتضمن('@'):
		نصية[] ئتص = نصئتصال.عووض('https://'، '').قسسم('@')؛
		نصية _ولوجيات = ئتص[0]؛
		نصية رابط = ئتص[1]؛
		نصية[] ولوجيات = _ولوجيات.قسسم(':')؛
		
		نوعبنية مدخ = {
			auth: {
				username: ولوجيات[0]،
				password: ولوجيات[1]
			}
		}؛
		
		هدا.بتش = _بتشدبي (ئسم: "https://{رابط}"، ئستيق: مدخ)؛
	وئلا:
		هدا.بتش = _بتشدبي (ئسم: نصئتصال)؛
	تم
تم

ئعلن بتشدبي.معلو() ك وعد معلوقاب:
	دع معلو = ريتما هدا.بتش['info']()؛
	ئرجع {
		ئسمقاب: معلو.db_name ك نصية،
		كموتيقة: معلو.doc_count ك عدد،
		مضيف: معلو.host ك نصية
	}؛
تم

ئعلن ئستنسخ (بتشدبي مصدر، بتشدبي هدف، خيائستنسخ خيا) ك وعد نوعبنية:
	دع مدخ = {}؛
	لكل مفتاح في نوعبنية.مفتاحين(خيا):
		نصية قمفتاح = _قموس[مفتاح]؛
		مدخ[قمفتاح] = خيا[مفتاح]؛
	تم
	دع نتج = ريتما _بتشدبي['replicate'](مصدر، هدف، مدخ)؛
	ئرجع {
		كمقرئ: نتج.docs_read،
		تمام: نتج.ok
	}
تم

ئعلن ئستنسخ_نصية (نصية مصدر، نصية هدف، خيائستنسخ خيا) ك وعد نوعبنية:
	دع مدخ = {}؛
	لكل مفتاح في نوعبنية.مفتاحين(خيا):
		نصية قمفتاح = _قموس[مفتاح]؛
		مدخ[قمفتاح] = خيا[مفتاح]؛
	تم
	دع نتج = ريتما _بتشدبي['replicate'](مصدر، هدف، مدخ)؛
	ئرجع {
		كمقرئ: نتج.docs_read،
		تمام: نتج.ok
	}
تم

ئعلن بتشدبي.ئستعلم (نوعبنية حدد، نصية[] حقلين؟، نصية[] رتتب؟، عدد ئحصر؟، عدد تجاوز؟) ك وعد نتجقاب:
	دع مدخ = {}؛
	
	مدخ.selector = حدد؛
	ئدا رتتب: 
		مدخ.sort = رتتب؛
	تم
	ئدا ئحصر: 
		مدخ.limit = ئحصر؛
	تم
	ئدا تجاوز: 
		مدخ.skip = تجاوز؛
	تم
	ئدا حقلين: 
		مدخ.fields = حقلين؛
	تم
	
	دع نتج = ريتما هدا.بتش['find'](مدخ)؛
	
	لكل عنصر في نتج.docs:
		عنصر._معرف = عنصر._id؛
		عنصر._نسخة = عنصر._rev؛
	تم
	
	ئرجع {
		وتيقات: نتج.docs ك نوعبنية[]،
		تحدير: نتج.warning ك نصية،
		ئحصاتنف: نتج.execution_stats ك نوعبنية،
		بوكمارك: نتج.bookmark ك نصية
	}؛
تم
