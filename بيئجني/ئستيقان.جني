ئورد تخزينجلسة، جيزن، مكون، مستند، عنصرمخصص من ئساسية؛
ئورد تطبيق من تطبيق؛

ئعلن ئستيقان ():
	يمدد مكون ()؛
تم

ئعلن ئستيقان.سججل ():
	عنصرمخصص.سججل("ئستيقان"، ئستيقان)؛
تم

ئعلن ئستيقان.صيير ():
	ئرجع (
		<قسم صنف="مخطط-طولي ئصفف-وسط ئرصف-وسط فجوة-5 ط-كامل فائض-مخفي">
			<قسم صنف="مخطط-عرضي ئرصف-وسط">
				<صورة مصدر="شعار.svg" عرض="130"></صورة>
			</قسم>
			
			<قسم صنف="مخطط-طولي فجوة-3 ع-96">
				<ئدخال نوع="نص" معرف="ئدخمستخدم" ستنب="ئسمستخدم" قيمة="" صنف="خلف-رمادي-700 نص-ئبيض ط-14 نصكجج"></ئدخال>
				<ئدخال نوع="كلمرور" معرف="ئدخمرور" ستنب="كلمتسر" قيمة="" صنف="خلف-رمادي-700 نص-ئبيض ط-14 نصكجج"></ئدخال>
				<حيز عندنقر="نقرستيقن" صنف="مئشر-سبابة نصوسط حشص-3 نص-ئبيض مقوس نصكجج خلف-ئحمر-600">ئستيقن</حيز>
			</قسم>
		</قسم>
		
		<قسم صنف="ئصفف-وسط ئرصف-وسط يخفرقن موضعمطلق تحت-3 نصوسط ع-شاشة">
			<قسم صنف="مخطط-طولي فجوة-1">
				<صورة مصدر="رقمين.png" نصف="شفافية75" عرض="60"></صورة>
			</قسم>
		</قسم>
	)؛
تم

ئعلن ئستيقان.نقرستيقن ():
	دع نفسه = مستند.ئستعلم((<ئستيقان>))؛
	دع غمازة = مستند.ئستعلم((<غمازة>))؛
	دع مدخمستخدم = مستند.ردعنصربمعرف('ئدخمستخدم')؛
	دع مدخكلمرور = مستند.ردعنصربمعرف('ئدخمرور')؛
	دع نتج = ريتما سياق.قاعدب.ئستعلم({
		حدد: {
			ئسمستخدم: مدخمستخدم.قيمة،
			كلمرور: مدخكلمرور.قيمة
		}
	})؛
	ئدا ليس نتج.وتيقات.طول:
		مدخكلمرور.قيمة = ''؛
		غمازة.حالة = {
			رسالة: 'معلومات الولوج غير صحيحات'
		}
		ئرجع
	تم
	دع مستخدم = نتج.وتيقات[0]؛
	تخزينجلسة.خدعنصر('مستيقن'، جيزن.ئلانص(مستخدم))؛
	مستند.ئستعلم((<تطبيق>)).حممل()؛
تم

