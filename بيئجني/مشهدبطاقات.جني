ئورد مكون، عنصرمخصص، عنصرئدخال، عنصر، عنصرئشتمل؛
ئورد النافدة من نافدة؛
ئورد المستند من مستند؛

ئورد نتجقاب، بتشدبي من مكون.بتشدبي؛
ئورد شبكي من مكون.شبكي؛
ئورد طافية من مكون.طافية؛

ئورد مشهدبطاقة؛

ئورد قابمحلي من قاعدب؛
ئورد مختطبطاقة من مختطة؛

ئعلن مشهدبطاقات ():
	يمدد مكون()؛
	هدا._حالة = {
		بطاقات: []
	}؛
تم

ئعلن مشهدبطاقات.حممل () ك وعد:
	نتجقاب نتج = ريتما قابمحلي.ئستعلم(
		حدد: {
			نوع: 'بطاقة'
		}
	)؛
	هدا.خدحالة({
		بطاقات: نتج.وتيقات
	})؛
تم

ئعلن مشهدبطاقات.عندنقرفتحبطاقة (عنصرئشتمل هدف) ك وعد:
	طافية طاف = هدا.ئستعلم((<طافية>))؛
	نصية معرفبطاقة = هدف.ردخاصية('بيا-معرف')؛
	مختطبطاقة بطاقتي = ريتما قابمحلي.ئجلب(معرفبطاقة)؛
	طاف.خدحالة({
		جسم: 'مشهدبطاقة'،
		بيا: {
			بطاقة: بطاقتي
		}
	})؛
تم

ئعلن مشهدبطاقات.صيير ():
	مختطبطاقة بطاقة؛
	نصية طنافدة = "height:%{النافدة.طولداخلي - 60}px"؛
	ئرجع (
		<قسم صنف="حشس-4 فائضس-مخفي فائضص-تلقا خلف-حيادي-900">
			<قسم سمة="%{طنافدة};touch-action:pan-y">
				<شبكي عموصغ="2" عموسط="3" عموكب="4" فجوة="10 10" ماسنري="">
					<تكرار لكل="بطاقة" في="هدا._حالة.بطاقات">
						<قسم عندنقر="عندنقرفتحبطاقة" بيا-معرف="${بطاقة._معرف}" صنف="مخطط-طولي فجوة-1 حشتح-4 مقوسكج ئطار1 حشس-3 ئطار-رمادي-600 تئشير:ضلملتف رمادي-500 ط-محتوا">
							<شرط>
								<عند تحقق="بطاقة.عنوان">
									<حيز صنف="نص-ئبيض مئشر-فتراضي هشفو-3">%{بطاقة.عنوان}</حيز>
								</عند>
							</شرط>
							<منسق صنف="حش-0 هشتح-0 هشفو-3 مئشر-فتراضي نصصغ نص-ئبيض فائض-مخفي منحيمين بنط-ئرابتيك" سمة="unicode-bidi:inherit;-moz-tab-size:1;-o-tab-size:1;tab-size:1;">%{بطاقة.محتوا.قططع\(0، 100\) + '...'}</منسق>
							<حيز مخطط-ئوسع="لايتمدد" صنف="نصصغ حشس-2 هشفو-2 نص-ئبيض ئطار1 مقوسكب ئطار-رمادي-600 ع-محتوا">%{بطاقة.وحدة}</حيز>
						</قسم>
					</تكرار>
				</شبكي>
			</قسم>
		</قسم>
		<طافية عنصر-بطاقة="خلف-حيادي-900" صنف="داكن" حجم="ملئ" تابتة></طافية>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مشهدبطاقات'، مشهدبطاقات)؛
تم
