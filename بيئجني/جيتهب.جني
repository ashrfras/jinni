ئورد الكل ك سياق من سياق؛
ئورد مصفبلش8، مفككترميز؛
ئورد ئجلب، جواب؛
ئورد مجال من مصفوفة؛
ئورد رجكس؛

ئعلن هلرابطصحيح (نصية رابط) ك نصية[]:
	رجكس رجك = رجكس('github\\.com\\/([^\\/]+)\\/([^\\/]+)')؛
	نصية[] طباقين = رابط.طابق(رجك)؛
	
	ئدا ليس طباقين:
		ئرجع عدم؛ #صيغة رابط غير صحيحة
	وئلا:
		ئرجع طباقين؛
	تم
تم

ئعلن ردمعلومستودع (نصية رابط، نصية توكن) ك وعد نوعبنية:
	نصية[] طباقين = هلرابطصحيح(رابط)؛
	
	ئدا ليس طباقين:
		ئرجع عدم؛
	تم
	
	نصية مالك = طباقين[1]؛
	نصية مستودع = طباقين[2]؛
	
	#ئستعلام برماجة جيتهب
	جواب جو = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}"،
		ترويسات: {
			'Authorization': "token _{توكن}"
		}
	)؛
	
	ئدا جو.ناجح:
		نوعبنية نتج = ريتما جو.جيزن()؛
		ئرجع {
			مالك،
			مستودع،
			وصف: نتج['description']،
			فرعمفترض: نتج['default_branch']
		}؛
	وئلا:
		ئرجع عدم؛
	تم
تم

ئعلن ردجميعملفات (نصية توكن، نصية مالك، نصية مستودع، نصية فرع) ك وعد نصية[]:
	#جلب معرف ئاخر ئيداع
	جواب جوئاخرئيداع = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/refs/heads/_{فرع}"،
		ترويسات: {
			'Authorization': "token _{توكن}"
		}
	)؛
	نوعبنية بيائاخرئيداع = ريتما جوئاخرئيداع.جيزن()؛
	نصية معرفئيداع = بيائاخرئيداع['object'].sha ك نصية؛
	
	#جلب معرف الشجرة
	جواب جوئيداع = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/commits/_{معرفئيداع}"،
		ترويسات: {
			'Authorization': "token _{توكن}"
		}
	)؛
	نوعبنية بيائيداع = ريتما جوئيداع.جيزن()؛
	نصية معرفشجرة = بيائيداع['tree'].sha ك نصية؛
	
	#جلب شجرة ملفات المستودع
	جواب جوشجرة = ريتما ئجلب(
		عنونت: "_{سياق.برماجيت}/_{مالك}/_{مستودع}/git/trees/_{معرفشجرة}?recursive=1"،
		ترويسات: {
			'Authorization': "token _{توكن}"
		}
	)؛
	نوعبنية بياشجرة = ريتما جوشجرة.جيزن()؛
	نوعبنية[] بيا = بياشجرة['tree']؛
	دع نتج = بيا.رششح(دالة (عنصر): عنصر['type'] == 'blob')؛
	ئرجع نتج؛
تم

ئعلن ئساس64ئلايوتف8 (ئساس64) ك نصية:
	نصية نصئتنائي = ئساس64.فكترميز64()؛
	عدد طول = نصئتنائي.طول؛
	
	مصفبلش8 بايتات = مصفبلش8(طول)؛
	لكل ع في مجال(0، طول):
		بايتات[ع] = نصئتنائي.ردرمزحرف(ع)؛
	تم
	
	مفككترميز مفك = مفككترميز('يوتف8')؛
	ئرجع مفك.فكترميز(بايتات)؛
تم

ئعلن ردمحتواملف (نصية توكن، نصية مالك، نصية مستودع، نصية مسار) ك وعد نصية:
	جواب جومحتوا = ريتما ئجلب(
		عنونت: "%{سياق.برماجيت}/%{مالك}/%{مستودع}/contents/%{مسار}"،
		ترويسات: {
			'Authorization': "token %{توكن}"
		}
	)؛
	نوعبنية بيامحتوا = ريتما جومحتوا.جيزن()؛
	ئرجع ئساس64ئلايوتف8(بيامحتوا['content'] ك نصية)؛
	#ئرجع (بيامحتوا['content'] ك نصية).فكترميز64()؛
تم
