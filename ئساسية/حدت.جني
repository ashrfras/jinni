ئورد Event من '//'؛
ئورد نصية من ئساسية.نصية؛
ئورد منطق من ئساسية.منطق؛
ئورد مصفوفة من ئساسية.مصفوفة؛
ئورد نوعبنية من ئساسية.نوعبنية؛

ئعلن _حدت (ئسم، خيا؟):
	يختصر Event؛
	يملك منوع بيا يختصر detail؛
	يملك منوع هدف يختصر target؛
تم

ئعلن _قمس = {'يتصاعد': 'bubbles'، 'يلغا': 'cancelable'، 'يجتاز': 'composed'، 'بيا': 'detail'}؛
ئعلن _ردمدخ (نوعبنية خيا) ك نوعبنية:
	ئدا ليس خيا:
		ئرجع عدم؛
	وئلا:
		دع مدخ = {}؛
		لكل مفتاح في نوعبنية.مفتاحين(خيا):
			نصية مف = _قمس[مفتاح]؛
			منوع قيمة = خيا[مفتاح]؛
			مدخ[مف] = قيمة؛
		تم
		ئرجع مدخ؛
	تم
تم

ئعلن حدت (نصية ئسم، بنية خيا؟ {منطق يتصاعد، منطق يلغا، منطق يجتاز، منوع بيا}):
	يمدد _حدت(ئسم، _ردمدخ(خيا))؛
تم

ئعلن _حدت.ئمنعمفترض ():
	يختصر preventDefault؛
تم

ئعلن _حدت.ردهدف () ك منوع:
	ئرجع هدا.هدف؛
تم

ئعلن _حدت.ردبيا () ك منوع:
	ئرجع هدا.بيا؛
تم

ئعلن ردئسمحدت (نصية ئسم) ك نصية:
	دع قموس = {
		خروجمئشر:'mouseleave'، لاتركيز:'blur'، تركيز:'focus'، تغيير:'change'،
		نقر:'click'، قائسياق:'contextmenu'، نقرتان:'dblclick'، ئغلاق:'close'،
		سحب:'drag'، نهايتسحب:'dragend'، دخولسحب:'dragenter'، خروجسحب:'dragleave'،
		سحبفوق:'dragover'، بدايتسحب:'dragstart'، ئفلات:'drop'، ئدخال:'input'،
		ضغطمفتاح:'keydown'، تركمفتاح:'keyup'، تحميل:'load'، نهايتحميل:'loadend'،
		بدتحميل:'loadstart'، دخولمئشر:'mouseenter'، عجلة:'wheel'، تمرير:'scroll'،
		تحجيم:'resize'، نهايتلمس:'touchend'، ئرسال:'submit'، بدايتلمس:'touchstart'، تحريكمئشر:'mousemove'
	}؛
	ئرجع (قموس[ئسم] ك نصية) ئو ئسم؛
تم
